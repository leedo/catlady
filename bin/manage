#!/usr/bin/env perl

use autodie;
use JSON::XS ();
use DBI;
use Digest::SHA1 ();

use v5.14;

my %commands = (
	adduser => \&adduser,
	resetpass => \&resetpass,
);

my ($command, @args) = @ARGV;

if ($command and exists $commands{$command}) {
	$commands{$command}->(@args);
}
else {
	die "invalid command\n";
}

sub config {
	my $config = do {
		local $/;
		open my $fh, "<", "config.json";
		<$fh>;
	};
	JSON::XS::decode_json $config;
}

sub dbh {
	DBI->connect(config->{dsn}, config->{db_user}, config->{db_pass});
}

sub salt {
	Digest::SHA1::sha1_hex join "-", shift, config->{salt};
}

sub genpass {
	join "", map { ("a".."z","A".."Z",0..9)[rand 62] } 0..8;
}


sub resetpass {
	my ($username, $password) = @_;
	$password = genpass unless defined $password;
	my $salted = salt $password;
	
	say "$username $password";
	
	dbh->do("UPDATE users SET password=? WHERE username=?", {}, $salted, $username);
}

sub adduser {
	my ($username, $password) = @_;
	$password = genpass unless defined $password;
	my $salted = salt $password;

	say "$username $password";

	dbh->do("INSERT INTO users (username, password) VALUES(?,?)", {}, $username, $salted);
}
